{
  'name' 'writeVar'
  'desc' 
  <'
    Used to write mono value GTS (on the 0 timestamp)
    The gts is found by its classname
  '>
  'sig' [ [ [ 'classname:STRING' ] [ 'value:<ANY>' ] ] ] // Signature
  'params' {
    // Signature params description
    'classname' 'classname of the mono GTS record sought'
    'value' 'value to write'
  }
  'examples' [
    <'
      'edgeDemo.testVariable' 24 @utils/writeVar
    '>
  ]
} 'info' STORE
<%
  SAVE 'context' STORE
  'varValue' STORE
  'varName' STORE

  { 
    'token' $readToken 
    'class' $varName
    'labels' {}
    'start' 0
    'end' 1
  } FETCH 'gts' STORE

  <% $gts SIZE 0 == %>
  <%
    $varName '.' SPLIT 'aClassname' STORE

    {} 'labels' STORE
    0 'i' STORE

    $aClassname
    <% 
      'element' STORE
      <% $i 0 > $aClassname SIZE 3 >= AND %>
      <% 
        $labels { 'l-' $i TOSTRING + $element } APPEND 'labels' STORE
      %>
      IFT
      $i 1 + 'i' STORE
    %>
    FOREACH

    NEWGTS $varName RENAME $labels RELABEL 0 NaN NaN NaN $varValue ADDVALUE $varName RENAME $writeToken UPDATE
    
  %>
  <%
    $gts 0 GET 0 REMOVETICK
    0 NaN NaN NaN $varValue ADDVALUE $varName RENAME $writeToken UPDATE
  %>
  IFTE

  $context RESTORE
%>
